       PUNCH 'SETCODE AC(1)'
*********************************************************************
*                                                                   *
*    THIS SUBROUTINE IS USED TO SET HALFWORD PREFIXED STRING        *
*    PASSED TO IT INTO A SET OF NAME/TOKEN PAIRS.  IT IS TO BE USED *
*    IN CONJUNCTION WITH INSTGET AND INSTUJI                        *
*                                                                   *
*    SEE INSTDOC FOR AN EXPLANATION OF THESE MACROS                 *
*                                                                   *
*********************************************************************
INSTSET  INSTSS
         L      R3,0(R1)     R3 -> STRING
         LH     R4,0(R3) R4 = LEN STRING
         LA     R4,2(R4)
*
* MOVE INTO BUFFER PADDING WITH LOWVALUES
*
         LA     R14,BUFARR
         L      R15,=A(BUFARRZ-BUFARR)
         LR     R0,R3
         LR     R1,R4
         MVCL   R14,R0
         MODESET MODE=SUP
         MVC    NAME,=C'INST.TTYY.ASIDXX'   TOKEN NAME SKEL
         LA     R1,0
         USING  PSA,R1
         L      R1,PSAAOLD
         DROP   R1
         USING  ASCB,R1
         MVC    NAME+14(2),ASCBASID         SET ASID IN HEX INTO NAME
         DROP   R1
*
*        DELETE ALL TOKENS CURRENTLY SET AND A FEW MORE
*
         LA     R2,0
         STCM   R2,3,NAME+7
         CALL   IEANTDL,(LEVEL,NAME,TOKEN,RETC),VL,MF=(E,PL)
DLOOP    EQU    *
         LA     R2,1(R2)
         STCM   R2,3,NAME+7
         CALL   IEANTDL,(LEVEL,NAME,TOKEN,RETC),VL,MF=(E,PL)
         LTR    R15,R15
         BZ     DLOOP
         LA     R2,1(R2)
         STCM   R2,3,NAME+7
         CALL   IEANTDL,(LEVEL,NAME,TOKEN,RETC),VL,MF=(E,PL)
         LTR    R15,R15
         BZ     DLOOP
         LA     R2,1(R2)
         STCM   R2,3,NAME+7
         CALL   IEANTDL,(LEVEL,NAME,TOKEN,RETC),VL,MF=(E,PL)
         LTR    R15,R15
         BZ     DLOOP
         LA     R2,1(R2)
         STCM   R2,3,NAME+7
         CALL   IEANTDL,(LEVEL,NAME,TOKEN,RETC),VL,MF=(E,PL)
         LTR    R15,R15
         BZ     DLOOP
         B      DLOOPZ
DLOOPZ   EQU    *
*
*        SET UP TOKENS, EACH 16 BYTES OF THE STRING
*        THE FIRST SET OF BYTES BLANK STOPS US DOING THIS
*
         LA     R2,0
         LA     R6,BUFARR
         LA     R4,BUFARR(R4)
TLOOP   EQU    *
         STCM   R2,3,NAME+7
         CALL   IEANTCR,(LEVEL,NAME,(6),PERSIST,RETC),VL,MF=(E,PL)
         LTR    R15,R15
         BNZ    ERRRE
         LA     R6,L'TOKEN(R6)
         LA     R2,1(R2)
         CR     R6,R4
         BH     TLOOPZ
         B      TLOOP
TLOOPZ   EQU    *
         MODESET MODE=PROB
         B      OK
*
*
ERRRE    EQU    *
         LR     R4,R15
         MODESET MODE=PROB
         LR     R15,R4
         B      ERR
*
ERR      EQU    *
         INSTRS RC=(R15)
*
OK       EQU    *
         INSTRS RC=0
*
LEVEL    DC     A(IEANT_SYSTEM_LEVEL)
         LTORG
PERSIST  DC     A(IEANT_PERSIST)


         INSTWORK  START

LBUFARR  DC  A(BUFARRZ-BUFARR)

PL       CALL   ,(,,,,,,,),MF=L

NAME     DC     CL16' '
TOKEN    DC     CL16' '
RETC     DC     A(0)

BUFARR   DS  0C
         DS  CL2000
BUFARRZ DS 0C

         INSTWORK  END


         IHAPSA  DSECT=YES
         IHAASCB DSECT=YES
         IEANTASM
         END
