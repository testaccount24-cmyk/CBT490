*********************************************************************
*                                                                   *
*    THIS SUBROUTINE IS USED TO GET A HALFWORD PREFIXED STRING      *
*    PASSED TO IT FROM A SET OF NAME/TOKEN PAIRS.  IT IS TO BE USED *
*    IN CONJUNCTION WITH INSTSET AND INSTUJI                        *
*                                                                   *
*    SEE INSTDOC FOR AN EXPLANATION OF THESE MACROS                 *
*                                                                   *
*********************************************************************
INSTGET  INSTSS
         L      R3,0(R1)     R3 -> REPLY
*
* MOVE INTO BUFFER PADDING WITH LOWVALUES
*
         MVC    NAME,=C'INST.TTYY.ASIDXX'   TOKEN NAME SKEL
         LA     R1,0
         USING  PSA,R1
         L      R1,PSAAOLD
         DROP   R1
         USING  ASCB,R1
         MVC    NAME+14(2),ASCBASID         GET ASID IN HEX INTO NAME
         DROP   R1
*
*        SET UP TOKENS, EACH 16 BYTES OF THE STRING
*        THE FIRST TOKEN WE DON'T GET STOPS US
*
         LA     R2,0
TLOOP   EQU    *
         STCM   R2,3,NAME+7
         CALL   IEANTRT,(LEVEL,NAME,TOKEN,RETC),VL,MF=(E,PL)
         LTR    R15,R15
         BNZ    TLOOPZ
         MVC    0(L'TOKEN,R3),TOKEN
         LA     R3,L'TOKEN(R3)
         LA     R2,1(R2)
         B      TLOOP
TLOOPZ   EQU    *
*
OK       EQU    *
         INSTRS RC=0
*
LEVEL    DC     A(IEANT_SYSTEM_LEVEL)
         LTORG
PERSIST  DC     A(IEANT_PERSIST)


         INSTWORK  START

PL       CALL   ,(,,,,,,,),MF=L

NAME     DC     CL16' '
TOKEN    DC     CL16' '
RETC     DC     A(0)

         INSTWORK  END


         IHAPSA  DSECT=YES
         IHAASCB DSECT=YES
         IEANTASM
         END
