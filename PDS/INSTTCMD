//TSO0571 JOB (T951V40),'INSTTCMD',MSGCLASS=T,CLASS=Z,NOTIFY=TSO057
// INCLUDE MEMBER=SYSLEV
//*
//*
//*        THIS JOB INSTALL THE MODULE INSTTCMD WHICH ALLOWS
//*        A JOB TO ISSUE ANY SYSTEM COMMAND
//*
//*        THIS MODULE IS AUTHORISED AND MUST BE RACF-PROTECTED AND ON
//*        A READ-PROTECTED LIBRARY SO IT CANNOT BE COPIED ELSEWHERE
//*        PROBABLY A LINKLIST OR LPALIST LIBRARY
//*
//*
//*        1) WILL WAIT 1 SEC BETWEEN COMMANDS
//*        2) WILL TREAT "//* " IN LINES 1-3 AS COMMENT
//*        3) WILL TREAT "//*WAIT HHMMSSTH" AS INSTR TO WAIT
//*        4) AFTER 100TH COMMAND WILL ONLY WAIT 1/4 SEC AND WILL
//*           ISSUE NO WTO.
//*
//ASM    EXEC PGM=ASMA90,PARM=(OBJ,NODECK)
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.&SYSLEV..AMODGEN,DISP=SHR
//         DD  DSN=SFT.INSTASM.ASM,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(9999,(600,100))
//SYSUT2   DD  DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD  DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSPRINT DD  SYSOUT=*,DCB=BLKSIZE=1089
//SYSPUNCH DD  SYSOUT=B
//SYSLIN   DD  DSN=&&OBJSET,UNIT=SYSSQ,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
INSTTCMD TITLE 'JLP ROUTINE FOR ISSUING SYSTEM COMMANDS USING SVC34'
*
***********************************************************************
*
*         MODULE NAME = INSTTCMD
*
***********************************************************************
         EJECT
INSTTCMD CSECT ,
         INSTREQU
         SAVE  (14,12),,INSTTCMD
         USING INSTTCMD,R12
         LR    R12,R15         SAVE BASE ADDRESS
         ST    R13,SAVE+4
         LA    R13,SAVE
GETPARM  EQU   *                    DO GET PARM
         L     R3,0(R1)             POINT TO PARM LENGTH
         LH    R4,0(R3)             LOAD PARM LENGTH
         LTR   R4,R4                TEST LENGTH
         BNP   NOPARM               SKIP IF LENGTH WAS 0
         LA    R3,2(R3)             R3 POINT TO COMMAND
         BCTR  R4,0                 DECREMENT LENGTH
         EX    R4,MVCCR3            MOVE PARM INTO COMTXT
         LA    R4,COMTXT-LENG+1(R4) RESTORE LENGTH + ADD PREFIX
         STH   R4,LENG              STORE IN LENGTH FIELD FOR SVC34
         BAL   R9,ISSUE             DO ISSUE COMMAND
NOPARM   EQU   *
         MVC   LENG,=Y(COMTXT-LENG+72) 72 BYTES INPUT FILE USED
         OPEN   (SYSIN,(INPUT))
         LA    R6,0
CMLOOP   EQU   *
         GET   SYSIN,COMTXT
         BAL   R9,ISSUE
         LA    R6,1(R6)
         B     CMLOOP
ENDFILE  EQU   *
         CLOSE  SYSIN
         FREEPOOL SYSIN
         L     R13,SAVE+4
         RETURN (14,12),RC=0

ISSUE    EQU   *

         CLC   =C'//*WAIT',COMTXT            IF //*WAIT COMMAND
         BNE   WAITZ
         CLI   COMTXT+8,C'0'
         BL    TRERR
         CLI   COMTXT+8,C'9'
         BH    TRERR
         CLI   COMTXT+9,C'0'
         BL    TRERR
         CLI   COMTXT+9,C'9'
         BH    TRERR
         CLI   COMTXT+10,C'0'
         BL    TRERR
         CLI   COMTXT+10,C'9'
         BH    TRERR
         CLI   COMTXT+11,C'0'
         BL    TRERR
         CLI   COMTXT+11,C'9'
         BH    TRERR
         CLI   COMTXT+12,C'0'
         BL    TRERR
         CLI   COMTXT+12,C'9'
         BH    TRERR
         CLI   COMTXT+13,C'0'
         BL    TRERR
         CLI   COMTXT+13,C'9'
         BH    TRERR
         CLI   COMTXT+14,C'0'
         BL    TRERR
         CLI   COMTXT+14,C'9'
         BH    TRERR
         CLI   COMTXT+15,C'0'
         BL    TRERR
         CLI   COMTXT+15,C'9'
         BH    TRERR
         CLC   COMTXT+8(8),=C'01000000'      1 HR MAX
         BH    TRERR
         MVC   TOWAIT,COMTXT+8
         STIMER WAIT,DINTVL=TOWAIT
         B     WAITZ
TRERR    EQU   *
         WTO   'INSTTCMD INVALID //*WAIT NNNNNNNN CARD'
WAITZ    EQU   *


         CLC   =C'//*',COMTXT                BYPASS IF JCL COMMENT
         BE    COMMENT
         CLC   =C'*',COMTXT                  BYPASS IF ASM COMMENT
         BE    COMMENT

COMMAND  EQU   *
         MODESET KEY=ZERO,MODE=SUP
         C     R6,=A(10000)
         BH    NOWTO

WTO      EQU   *
         STIMER WAIT,DINTVL=ONESEC

         CLC   COMTXT,=CL255' '
         BE    NOCMD
         XR    R0,R0
         LA    R1,LENG
         SVC   34
         MVC   WTOI+20(20),COMTXT
         WTO   MF=(E,WTOI)
NOCMD    EQU   *
         B     COMMANDZ

NOWTO    EQU   *
         STIMER WAIT,DINTVL=QTRSEC
         CLC   COMTXT,=CL255' '
         BE    NOCMD1
         XR    R0,R0
         LA    R1,LENG
         SVC   34
NOCMD1   EQU   *

COMMANDZ EQU   *
         MODESET KEY=NZERO,MODE=PROB

COMMENT  EQU   *
         BR    R9

SAVE     DC    18F'0'

SYSIN    DCB   DDNAME=SYSIN,                                           X
               LRECL=80,RECFM=FB,                                      X
               DSORG=PS,                                               X
               EODAD=ENDFILE,                                          X
               MACRF=GM

LENG     DC    H'100'
         DC    H'0'
COMTXT   DC    CL100' '

WTOI     WTO   'JOB HAS ISSUED                           COMMAND',MF=L
MVCCR3   MVC   COMTXT(0),0(R3)
         DS    0D
ONESEC   DC    C'00000100'
         DS    0D
QTRSEC   DC    C'00000025'
         DS    0D
TOWAIT   DC    C'00000000'


         END
//LKED    EXEC PGM=IEWL,PARM=(XREF,LIST),
//             COND=(4,LT,ASM)
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSPRINT DD  SYSOUT=*,DCB=(RECFM=FB,LRECL=121,BLKSIZE=1210)
//SYSLMOD  DD  DSN=YOUR.LINK.OR.LPA.LIB,DISP=SHR
   SETCODE AC(1)
   NAME  INSTTCMD(R)
