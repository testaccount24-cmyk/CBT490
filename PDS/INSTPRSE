          PRINT NOGEN
**********************************************************************
*                                                                    *
*         INSTPRSE IS A GENERAL PURPOSE PARSING ROUTINE              *
*                                                                    *
*         INPUT PARAMETERS ARE                                       *
*                                                                    *
*         1) Y(L'FIELD)                                              *
*         2) PARSING DELIMITER CHARACTERS (PRECEDED BY HALFWORD      *
*            WITH NUMBER OF CHARS IN)                                *
*         3) Y(NO. REC FIELDS,L'REC FIELD 1,L'REC FIELD 2, ..)       *
*         4) FIELD                                                   *
*         5) REC FIELD 1                                             *
*         6) REC FIELD 2                                             *
*         .....                                                      *
*         AND SO ON ....                                             *
*                                                                    *
*                                                                    *
**********************************************************************
INSTPRSE  INSTSS
          XC    RC,RC
          XC    NX,NX
          XC    X,X
          LR    R8,R1
          L     R1,0(R8)
          LTR   R1,R1
          BNP   Z012
          LH    R1,0(R1)
          LTR   R1,R1
          BM    Z012
          L     R1,4(R8)
          LTR   R1,R1
          BNP   Z012
          LH    R1,0(R1)
          LTR   R1,R1
          BM    Z012
          L     R1,8(R8)
          LTR   R1,R1
          BNP   Z012
          LH    R1,0(R1)
          LTR   R1,R1
          BM    Z012
          L     R1,12(R8)
          LTR   R1,R1
          BNP   Z012
          L     R2,8(R8)              R2->REC FIELD LENGTHS
          LA    R9,2(R2)              R9 ->LEN REC FIELD 1
          LH    R2,0(R2)              R2=NO OF FIELDS
          LTR   R2,R2
          BZ    A200                  IF NOT 0 FIELDS
          LA    R10,16(R8)            R10->ADD REC FIELD 1
A100      EQU   *                     LOOP TO CLEAR ONE FIELD
          LH    R15,0(R9)             R15=LEN REC FIELD
          LTR   R15,R15
          BM    Z012
          L     R14,0(R10)            R14->REC FIELD
          LA    R0,*
          L     R1,=A(X'40000000')    FOR PAD
          MVCL  R14,R0                CLEAR
          LA    R10,4(R10)            NEXT FIELD
          LA    R9,2(R9)              NEXT LENGTH
          BCT   R2,A100            ENDLOOP TO CLEAR ONE FIELD
A200      EQU   *                     ENDIF
          L     R2,4(R8)              R2->DELIMITERS
          XC    X,X                   CLEAR XLATE TAB
          LR    R9,R2                 R9-> FIRST FIELD - 2
          LH    R2,0(R2)              R2=NO OF DELIMS
          LTR   R2,R2
          BZ    A275                  IF NOT 0 DELIMS
          SLR   R1,R1
          IC    R3,=X'FF'
A250      EQU   *                     LOOP TO PUT ONE DELIMITER IN
          IC    R1,1(R9,R2)
          STC   R3,X(R1)              MOVE IN ONE Â¬ZERO CHAR
          BCT   R2,A250            ENDLOOP
A275      EQU   *                     ENDIF
          MVI   NX,X'FF'
          MVC   NX+1(L'NX-1),NX       FILL INVERSE XLATE TAB.
          XC    NX,X                  XLATE TAB FOR NON-DELIMS
          XC    RC,RC                 0 RC
          L     R4,12(R8)             R4->FIELD POINTER
          L     R5,0(R8)              R5->FIELD LEN
          LH    R5,0(R5)              R5=FIELD LEN LEFT TO DO
          AR    R5,R4                 R5-> END FIELD
          L     R6,8(R8)              R6->REC FIELD LENGTHS
          LH    R7,0(R6)              R7=NO OF REC FIELDS LEFT
          LA    R6,2(R6)              R6->REC FIELD LENGTH 1
          LA    R10,16(R8)            R10->ADD REC FIELD 1
A300      EQU   *                     LOOP TO PARSE TO ONE REC FIELD
          CR    R4,R5                 LEAVE WHEN FIELD DONE
          BNL   A500
          LTR   R7,R7                 LEAVE WHEN NO REC FIELDS LEFT
          BZ    A500
A350      EQU   *                     LOOP TO XLATE 256 BYTES
          LA    R1,256(R4)
          LR    R11,R5                *
          SR    R11,R4                *  WANT TO LOOK AT THE NEXT
          BCTR  R11,0                 *  256 BYTES OF THE INPUT
          C     R11,=F'255'           *  FIELD, OR IF NOT THAT
          BNH   A351                  *  MUCH, THEN AS MUCH AS
          L     R11,=F'255'           *  THERE IS
A351      EX    R11,TRT1              *
          BNZ   A370                  LEAVE WHEN FOUND
          CR    R1,R5
          BH    A370                  LEAVE WHEN PAST END
          LA    R4,256(R4)
          B     A350
A370      EQU   *                     ENDLOOP
          LR    R4,R1
          CR    R4,R5
          BNH   A390
          LR    R4,R5
A390      EQU   *
          LR    R14,R4
A400      EQU   *
          LA    R1,256(R4)
          LR    R11,R5                *
          SR    R11,R4                *  WANT TO LOOK AT THE NEXT
          BCTR  R11,0                 *  256 BYTES OF THE INPUT
          C     R11,=F'255'           *  FIELD, OR IF NOT THAT
          BNH   A401                  *  MUCH, THEN AS MUCH AS
          L     R11,=F'255'           *  THERE IS
A401      EX    R11,TRT2              *
          BNZ   A420
          CR    R1,R5
          BH    A420
          LA    R4,256(R4)
          B     A400
A420      EQU   *
          LR    R4,R1
          CR    R4,R5
          BNH   A440
          LR    R4,R5
A440      EQU   *
          LR    R15,R4
          SR    R15,R14
          L     R0,0(R10)
          LH    R1,0(R6)
          CR    R1,R15
          BNL   A450
          MVC   RC,=A(4)
A450      EQU   *
          A     R15,=A(X'40000000')
          MVCL  R0,R14
          BCTR  R7,0                  ONE REC FIELD DONE
          LA    R10,4(R10)            R10->ADD NEXT REC FIELD
          LA    R6,2(R6)              R6->NEXT REC FIELD LENGTH
          B     A300
A500      EQU   *                  ENDLOOP TO PARSE TO ONE REC FIELD
          CLC   RC,=A(0)
          BE    RET0
RET4     INSTRS RC=4
RET0     INSTRS RC=0
Z012     INSTRS RC=12
          LTORG
TRT1      TRT   0(0,R4),NX
TRT2      TRT   0(0,R4),X
          INSTWORK   START
RC        DC    A(0)
X         DC    XL256'00'
NX        DC    XL256'00'
          INSTWORK   END
          END
