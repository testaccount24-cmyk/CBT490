       MACRO
.**********************************************************************
.*
.*  12/12/00  1.0    WRITTEN                                     AWR *
.*
.**********************************************************************
.*
.*
.* START RENT ASM PROGRAM
.*
.* AFTER THIS R12 IS THE BASE ON THE PROGRAM CSECT, R13 THE BASE
.* ON THE GETMAINED DSECT FOR WORKING STORAGE.  ANY FURTHUR USINGS
.* MUST BE SET UP MANUALLY
.*
.*
.*
&NAME  INSTSS &VER=1,&SAVEAREA=YES

.*
.* FOR INSTBALI/O CELLS
.*
       GBLA   &INSTBALN
       GBLB   &INSTSAVE
&INSTBALN SETA   0 NO OF BAL CELLS
&INSTSAVE SETB   0 GETMAIN SAVE AND WORK AREA?

         AIF   ('&SAVEAREA' EQ 'YES').SAVEOK
         AIF   ('&SAVEAREA' EQ 'NO').SAVEOK
         MNOTE 8,'SAVEAREA MUST BE YES OR NO'
         MEXIT
.SAVEOK  ANOP

         AIF   ('&NAME' NE '').NAMEOK
         MNOTE 8,'NAME MUST BE SPECIFIED'
         MEXIT
.NAMEOK  ANOP

&NAME  CSECT
&NAME  AMODE 31
&NAME  RMODE ANY
       INSTREQU
       USING *,R15
       SAVE (14,12),,'&NAME VER &VER &SYSDATE &SYSTIME '
       DROP  R15
       USING &NAME,R12         SET UP CSECT ADDRESSABILITY
       LR    R12,R15             LOAD BASE REGISTER


.*     SAVEAREA = NO MEANS NO WORK AREA/SAVE AREA GETMAINED

       AIF   ('&SAVEAREA' EQ 'NO').MEXIT



&INSTSAVE SETB   1 GETMAIN SAVE AND WORK AREA?


       L     R0,INSTWORK_DATALEN_A
       GETMAIN RU,LV=(0),LOC=BELOW   STORAGE FOR DATA DSECT, ADDR IN R1
       USING INSTWORK_DATA,R1
       ST    R13,INSTWORK_SAVEBK CHAIN BACK
       ST    R1,INSTWORK_SAVEFW-INSTWORK_SAVE(R13) CHAIN FOREWARD
       LR    R15,R1
       LM    R0,R1,INSTWORK_SAVER0-INSTWORK_SAVE(R13) RESTORE R0 R1
       LR    R13,R15             LOAD REGISTER 13 ADDR GETMAINED SA
       DROP  R1
       USING INSTWORK_DATA,R13   SET UP DATA DSECT ADDR.
       B     *+8
INSTWORK_DATALEN_A DC A(INSTWORK_DATALEN)

.MEXIT ANOP
       MEND
