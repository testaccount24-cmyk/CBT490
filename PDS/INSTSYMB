***********************************************************************
*                                                                     *
*   INSTSYMB IS USED TO CALL ASASYMBU TO DO SUBSTITUTIONS             *
*                                                                     *
*                1)   CONTROL BYTE                                    *
*                2)   CL255 SUBSTUTUTE STRING                         *
*                3)   CL255 SUBSTUITUTED STRING                       *
*                4)   REPLY Y OR N                                    *
*                                                                     *
*   THE ROUTINE IS REENTRANT, 31/ANY AND LPA RESIDENT                 *
*                                                                     *
***********************************************************************
INSTSYMB INSTSS

         LR    R6,R1       SAVE R1
         L     R1,00(R6)   GET PATTERN

         CLI   0(R1),C'1' ONLY TYPE 1 ALLOWED SO FAR
         BE    TYPE1

         B     FAIL

TYPE1    DS    0H
         L     R1,04(R6)   GET PATTERN
         MVC   PATTERN,0(R1)
*
*        CALL ASASYMBU TO DO SUBSTITUTION
*
*        SET UP CONTROL BLOCK
*
         LA    R2,MYSYMBP
         USING SYMBP,R2
         XC    SYMBP(SYMBP_LEN),SYMBP   INITIALIZE TO ZERO
         LA    R1,PATTERN               ADDRESS OF PATTERN
         ST    R1,SYMBPPATTERN@         SAVE IN SYMBP AREA
         LA    R1,L'PATTERN             LENGTH OF PATTERN
         ST    R1,SYMBPPATTERNLENGTH    SAVE IN SYMBP AREA
         MVC   TARGET,=CL255' '
         LA    R1,TARGET                ADDRESS OF TARGET
         ST    R1,SYMBPTARGET@          SAVE IN SYMBP AREA
         MVC   TARGETLENGTH,=A(L'TARGET)  SET LENGTH OF TARGET
         LA    R1,TARGETLENGTH          ADDRESS OF TARGET LENGTH
         ST    R1,SYMBPTARGETLENGTH@    SAVE IN SYMBP AREA
         LA    R1,RETURNCODE            ADDRESS OF RETURN CODE
         ST    R1,SYMBPRETURNCODE@      SAVE IN SYMBP AREA

         LA    R1,MYSYMBT               SYMBOL TABLE
         ST    R1,SYMBPSYMBOLTABLE@     SAVE IN SYMBP AREA
         DROP R2

         LA    R2,MYSYMBT               SYMBOL TABLE
         USING SYMBT,R2
         XC    SYMBTHEADER,SYMBTHEADER
         OI    SYMBTFLAGS,SYMBTWARNNOSUB WARN IF NO SUB
         LA    R1,0                    NUMBEROFSYMBOLS
         STH   R1,SYMBTNUMBEROFSYMBOLS SAVEINSYMBTAREA
         DROP   R2

  LINK   EP=ASASYMBM,MF=(E,MYSYMBP),SF=(E,LINKL),DCB=0
         L     R2,08(R6)        R2 -> RETURNED VALUE
         MVC   0(L'TARGET,R2),TARGET
         CLC   =A(0),RETURNCODE
         BNE    FAIL
         B     OK





OK       EQU   *
         L     R1,12(R6)
         MVI 0(R1),C'Y'
         INSTRS


FAIL     EQU   *
         L     R1,12(R6)
         MVI 0(R1),C'N'
         INSTRS  RC=0




         INSTWORK START

LINKL        LINK SF=L
PL           CALL MF=L

PATTERN      DS    CL255

             DS    0F
RETURNCODE   DS    F                   RETURN CODE
TARGETLENGTH DS    F                   LENGTH OF TARGET
TARGET       DS    CL255
             DS    CL255 BUFFER

             DS    0D
MYSYMBP      DS    CL(SYMBP_LEN)       SYMBP AREA


             DS    0D
MYSYMBT      DS    CL(SYMBT_LEN)

        INSTWORK END

             ASASYMBP                  MAPPING OF SYMBP AREA
        END
