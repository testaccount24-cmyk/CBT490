**********************************************************************
*
*  12/12/00  1.0    WRITTEN                                     AWR *
*
**********************************************************************

*******************************************************************
*                                                                 *
*        THIS IS AN ISO / DAYS CONVERSION                         *
*        ROUTINE.                                                 *
*                                                                 *
*                                                                 *
*        THREE PARAMETERS:                                        *
*                                                                 *
*            1)  '1' FOR ISO->DAYS, '2' FOR DAYS->ISO             *
*                                                                 *
*            2)  THE ISO DATE 'CCYY-MM-DD'   -  MUST HAVE DASHES  *
*                NOT BLANKS OR ANY OTHER SEPARATOR                *
*                                                                 *
*            3)  A FULLWORD HOLDING THE (DAYS_SINCE_1_JAN_1_AD)+1 *
*                                                                 *
*                                                                 *
*               This number is is of course meaningless           *
*               before the date adjustments of the middle         *
*               ages, so it is best defined as                    *
*                                                                 *
*                01.01.2000 = 730120                              *
*                                                                 *
*                                                                 *
*                                                                 *
*                                                                 *
*        It is reentrant and contains no SVCs                     *
*        and also bimodal (able to run in 24 or 31 bit mode)      *
*                                                                 *
*                                                                 *
*                                                                 *
*******************************************************************
ISO      DSECT
ISOCCYY  DS   0CL4
ISOCC    DS    CL2
ISOYY    DS    CL2
ISODSH1  DS    C
ISOMM    DS    CL2
ISODSH2  DS    C
ISODD    DS    CL2
*
DAY      DSECT
DAYA     DS    A
*
FLAG     DSECT
FLAGC    DS    C
*
         INSTREQU
*
INSTDTID INSTSS SAVEAREA=NO
         LM    R9,R11,0(R1)    GET PARAMETER ADDRESSES
         USING FLAG,R9
         USING ISO,R10
         USING DAY,R11
         CLI   FLAGC,C'1'
         BE    ISOTOD
         CLI   FLAGC,C'2'
         BE    DTOISO
         B     RETBAD
*
LASTDAY  EQU   3652059
*
DTOISO   EQU   *
*
* TO CHANGE (DAYS_SINCE_1_JAN_1_AD)+1 TO A YEAR-MONTH-DAY
* DATES GO IN A 400 YEAR CYCLE.  WE DECOMPOSE THE DAY INTO A FINITE
* NUMBER OF 400 YEAR PERIODS, FOLLOWED BY 0-3 100YEAR PERIODS,
* FOLLOWED BY 0-24 4-YEAR PERIODS, FOLLOWED BY 0-3 YEARS, FOLLOWED BY
* A NUMBER OF DAYS.
*
* CHECK DAYS BETWEEN 1 AND 3652059 (= 9999-12-31)
*
         L     R1,DAYA
         C     R1,=A(1)
         BL    RETBAD
         C     R1,=A(LASTDAY)
         BH    RETBAD
*
* DECOMPOSE INTO 400YR, 100YR, 4YR, YEAR, DAYSLEFT
*
*
         BCTR  R1,0            R1 = DAYS- 1
*
DAYS4Y    EQU 365*4+1
DAYS100Y  EQU DAYS4Y*25-1
DAYS400Y  EQU DAYS100Y*4+1
*
         SLR   R0,R0           R0R1 = DAYS - 1
         D     R0,=A(DAYS400Y)
*                              R1 = NO 400 YR PERIODS SINCE 1 JAN 0001
*                              R0 = DAYS LEFT OVER
*
         LR    R3,R0
         SLR   R2,R2           R2R3 = DAYS LEFT OVER
         D     R2,=A(DAYS100Y)
         CH    R3,=Y(4)        IF 4 100YR PERIODS
         BL    BB001
         LA    R3,3            REDUCE TO 3
         A     R2,=A(DAYS100Y) ADD A 100YR PERIOD TO DAYS LEFT OVER
BB001    EQU   *
*                              R3 = NO 100 YR PERIODS SINCE LAST WHOLE
*                                   400 YR PERIOD
*                              R2 = DAYS LEFT OVER
*
         LR    R5,R2
         SLR   R4,R4           R4R5 = DAYS LEFT OVER
         D     R4,=A(DAYS4Y)
*                              R5 = NO 4 YR PERIODS SINCE LAST WHOLE
*                                   100 YR PERIOD
*                              R4 = DAYS LEFT OVER
*
         LR    R7,R4
         SLR   R6,R6           R6R7 = DAYS LEFT OVER
         D     R6,=A(365)
         CH    R7,=Y(4)        IF 4 YEARS
         BL    BB002
         LA    R7,3            REDUCE TO 3
         A     R6,=A(365)      ADD A YEAR TO DAYS LEFT OVER
BB002    EQU   *
*                              R7 = NO YEARS SINCE LAST WHOLE
*                                   4 YR PERIOD
*                              R6 = DAYS LEFT OVER
*
* STORE YEAR IN R8
*
         LR    R8,R1
         MH    R8,=Y(400)
         LR    R0,R3
         MH    R0,=Y(100)
         AR    R8,R0
         LR    R0,R5
         MH    R0,=Y(4)
         AR    R8,R0
         AR    R8,R7
         AH    R8,=Y(1)        R8 = YEAR
*
* NOW POINT R1 TO A TABLE OF DAYS IN
* MONTH
*
         LA    R1,MONTHT       ASSUME NOT LEAP YEAR
         CH    R7,=Y(3)        IS IT LAST YEAR OF 4-YR CYCLE
         BNE   BB0031          NO -> NOT LEAP YEAR
         CH    R5,=Y(24)       IS IT LAST 4-YR OF A 100-YR CYCLE?
         BNE   BB003           NO -> LEAP YEAR
         CH    R3,=Y(3)        IS IT LAST 100-YR OF 400-YR CYCLE
         BNE   BB0031          NO -> NOT LEAP YEAR
BB003    EQU   *
         LA    R1,MONTHTL      IT'S A LEAP YEAR.
BB0031   EQU   *
*
JAND     EQU   31
MARD     EQU   31
APRD     EQU   30
MAYD     EQU   31
JUND     EQU   30
JULD     EQU   31
AUGD     EQU   31
SEPD     EQU   30
OCTD     EQU   31
NOVD     EQU   30
DECD     EQU   31
*
*        SET R2 AS MONTH COUNTER
*
         LA    R2,1
BB005    EQU   *
         CH    R2,=Y(12)
         BH    RETBAD
         CH    R6,0(R1)
         BL    BB006
         SH    R6,0(R1)         REDUCE NO OF DAYS LEFT
         LA    R2,1(R2)         SKIP UP
         LA    R1,2(R1)         SKIP UP MONTH TAB
         B     BB005
BB006    EQU   *
         LA    R6,1(R6)
*
*        R8 = YYYY
*        R2 = MONTH
*        R6 = DAYS
*
*        PUT EM BACK IN DECIMAL WITHOUT ANY WORKING STORAGE TO USE!
*
         LR    R0,R6
         SRDL  R0,32
         D     R0,=A(10)
         STC   R0,ISODD+1
         STC   R1,ISODD
         OC    ISODD,=X'F0F0'
         LR    R0,R2
         SRDL  R0,32
         D     R0,=A(10)
         STC   R0,ISOMM+1
         STC   R1,ISOMM
         OC    ISOMM,=X'F0F0'
         LR    R0,R8
         SRDL  R0,32
         D     R0,=A(10)
         STC   R0,ISOCCYY+3
         SLR   R0,R0
         D     R0,=A(10)
         STC   R0,ISOCCYY+2
         SLR   R0,R0
         D     R0,=A(10)
         STC   R0,ISOCCYY+1
         STC   R1,ISOCCYY
         OC    ISOCCYY,=X'F0F0F0F0'
         MVI   ISODSH1,C'-'
         MVI   ISODSH2,C'-'
         B     RET0
*
MONTHT  DC Y(JAND,28,MARD,APRD,MAYD,JUND,JULD,AUGD,SEPD,OCTD,NOVD,DECD)
MONTHTL DC Y(JAND,29,MARD,APRD,MAYD,JUND,JULD,AUGD,SEPD,OCTD,NOVD,DECD)
*
*
*         PROCEDURE FOR YYYY-MM-DD TO A(DAY)
*
*
*
ISOTOD   EQU   *
*        CHECK INPUT HAS VALID CHARACTERS
*
         CLI   ISODSH1,C'-'
         BNE   RETBAD
         CLI   ISODSH2,C'-'
         BNE   RETBAD
         CLI   ISOCCYY+0,C'0'
         BL    RETBAD
         CLI   ISOCCYY+0,C'9'
         BH    RETBAD
         CLI   ISOCCYY+1,C'0'
         BL    RETBAD
         CLI   ISOCCYY+1,C'9'
         BH    RETBAD
         CLI   ISOCCYY+2,C'0'
         BL    RETBAD
         CLI   ISOCCYY+2,C'9'
         BH    RETBAD
         CLI   ISOCCYY+3,C'0'
         BL    RETBAD
         CLI   ISOCCYY+3,C'9'
         BH    RETBAD
         CLI   ISOMM+0,C'0'
         BL    RETBAD
         CLI   ISOMM+0,C'9'
         BH    RETBAD
         CLI   ISOMM+1,C'0'
         BL    RETBAD
         CLI   ISOMM+1,C'9'
         BH    RETBAD
         CLC   ISOMM,=C'12'
         BH    RETBAD
         CLC   ISOMM,=C'01'
         BL    RETBAD
         CLI   ISODD+0,C'0'
         BL    RETBAD
         CLI   ISODD+0,C'9'
         BH    RETBAD
         CLI   ISODD+1,C'0'
         BL    RETBAD
         CLI   ISODD+1,C'9'
         BH    RETBAD
         CLC   ISODD,=C'01'
         BL    RETBAD
*
* CHECK FOR UPPER BOUNDS VALID DAY WILL BE DONE LATER.
*
* NOW GET YEAR IN R5
*
         SLR   R2,R2
         IC    R2,ISOCCYY
         SH    R2,=Y(X'F0')
         MH    R2,=Y(1000)
         LR    R5,R2
         SLR   R2,R2
         IC    R2,ISOCCYY+1
         SH    R2,=Y(X'F0')
         MH    R2,=Y(100)
         AR    R5,R2
         SLR   R2,R2
         IC    R2,ISOCCYY+2
         SH    R2,=Y(X'F0')
         MH    R2,=Y(10)
         AR    R5,R2
         SLR   R2,R2
         IC    R2,ISOCCYY+3
         SH    R2,=Y(X'F0')
         AR    R5,R2
*
* R5 = YEAR
*
* SET UP R6 AS "LEAP"  =  (YEAR%4) - (YEAR%100) + (YEAR%400)
*
* % = DIVIDE AND DISCARD REMAINDER
*
         SLR   R6,R6
         SLR   R0,R0
         LR    R1,R5
         D     R0,=A(4)
         AR    R6,R1
         SLR   R0,R0
         LR    R1,R5
         D     R0,=A(100)
         SR    R6,R1
         SLR   R0,R0
         LR    R1,R5
         D     R0,=A(400)
         AR    R6,R1
*
* POINT R3 TO A TABLE OF DAYS-FROM-MAR1-OF-THIS-YEAR
* POINT R4 TO A TABLE OF DAYS-IN-MONTH
*
* THERE ARE 2 TABLES, ONE FOR LEAP YEARS, ONE FOR YEARS
*
         LA    R3,OFFT
         LA    R4,MONTHT
*
*        IS IT LEAP YEAR?
*
         SLR   R0,R0
         LR    R1,R5
         D     R0,=A(4)
         LTR   R0,R0                     DIV BY 4 ?
         BNZ   C0002                     NO -> NO LEAP YEAR
         SLR   R0,R0
         LR    R1,R5
         D     R0,=A(400)
         LTR   R0,R0                     DIV BY 400
         BZ    C0001                     YES-> LEAP YEAR
         SLR   R0,R0
         LR    R1,R5
         D     R0,=A(100)
         LTR   R0,R0                     DIV BY 100
         BZ    C0002                     YES-> NO LEAP YEAR
C0001    DS    0H
         LA    R3,OFFTL
         LA    R4,MONTHTL
C0002    DS    0H
*                                        GET MONTH IN R7
         SLR   R2,R2
         IC    R2,ISOMM
         SH    R2,=Y(X'F0')
         MH    R2,=Y(10)
         LR    R7,R2
         SLR   R2,R2
         IC    R2,ISOMM+1
         SH    R2,=Y(X'F0')
         AR    R7,R2                       R7 = MM
*                                         POINT TO ENTRIES FOR MONTH
         AR    R3,R7
         AR    R3,R7
         S     R3,=A(2)                   R3 -> CORRECT ENTRY
         AR    R4,R7
         AR    R4,R7
         S     R4,=A(2)                   R4 -> CORRECT ENTRY
*
*    GET THE DAY OF THE MONTH IN R8
*
         SLR   R2,R2
         IC    R2,ISODD
         SH    R2,=Y(X'F0')
         MH    R2,=Y(10)
         LR    R8,R2
         SLR   R2,R2
         IC    R2,ISODD+1
         SH    R2,=Y(X'F0')
         AR    R8,R2                       R8 = DD
*                                          IS MONTH TOO HIGH?
         CH    R8,0(R4)
         BH    RETBAD                     -> EXIT
*                                         GET DAYS SINCE 0001-01-01
         LR    R1,R5                      YEAR
         SLR   R0,R0
         M     R0,=A(365)                 R0R1 = YEAR*365
         AR    R1,R6                      + LEAP
         AH    R1,0(R3)                   + DAYS SINCE MAR1
         AR    R1,R8                      + DD
         S     R1,=A(306)                 -306
         ST    R1,DAYA
         B     RET0
*
OFFT     DS    0Y
         DC    Y(-(28+JAND))
         DC    Y(-28)
         DC    Y(0)
         DC    Y(MARD)
         DC    Y(MARD+APRD)
         DC    Y(MARD+APRD+MAYD)
         DC    Y(MARD+APRD+MAYD+JUND)
         DC    Y(MARD+APRD+MAYD+JUND+JULD)
         DC    Y(MARD+APRD+MAYD+JUND+JULD+AUGD)
         DC    Y(MARD+APRD+MAYD+JUND+JULD+AUGD+SEPD)
         DC    Y(MARD+APRD+MAYD+JUND+JULD+AUGD+SEPD+OCTD)
         DC    Y(MARD+APRD+MAYD+JUND+JULD+AUGD+SEPD+OCTD+NOVD)
OFFTL    DS    0Y
         DC Y(-(29+JAND))
         DC Y(-29)
         DC    Y(0)
         DC    Y(MARD)
         DC    Y(MARD+APRD)
         DC    Y(MARD+APRD+MAYD)
         DC    Y(MARD+APRD+MAYD+JUND)
         DC    Y(MARD+APRD+MAYD+JUND+JULD)
         DC    Y(MARD+APRD+MAYD+JUND+JULD+AUGD)
         DC    Y(MARD+APRD+MAYD+JUND+JULD+AUGD+SEPD)
         DC    Y(MARD+APRD+MAYD+JUND+JULD+AUGD+SEPD+OCTD)
         DC    Y(MARD+APRD+MAYD+JUND+JULD+AUGD+SEPD+OCTD+NOVD)
*
*
*
*
*
RET0     INSTRS RC=0
RETBAD   INSTRS RC=8
         END
