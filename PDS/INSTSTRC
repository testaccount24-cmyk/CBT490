INSTSTRC INSTSS SAVEAREA=NO
**********************************************************************
*
*  12/12/00  1.1    WRITTEN                                     AWR *
*
**********************************************************************
* **********************************************************
* *          *                                             *
* *   INSTSTRC IS CALLED BY THE MACRO INSTSTR  TO DO ANY   *
* *   COMPLEX EDITING.                                     *
* *          *                                             *
* *   ON INPUT THERE IS ONE PARAM IN THE FORMAT OF         *
* *   INSTSDSC                                             *
* *          *                                             *
* **********************************************************
         L     R3,0(R1)
         USING INSTSTR_SDSC,R3
         CLI   INSTSTR_SFLG,INSTSTR_SED1 WHEN EDIT IN WITH LEN
         BNE   A100
         MVC   INSTSTR_EDWK,EDM         MOVE IN EDIT MASK
         LA    R1,INSTSTR_EDWK+L'INSTSTR_EDWK-1
         EDMK  INSTSTR_EDWK,INSTSTR_SPNP EDIT IN FROM PACKED FIELD
         BNM   A020                     IF -VE
         BCTR  R1,0
         MVI   0(R1),C'-'               MOVE IN -VE SIGN
A020     EQU   *                        ENDIF
         LA    R5,INSTSTR_EDWK+L'INSTSTR_EDWK R5->END EDIT FIELD
         L     R1,INSTSTR_SFLN          R1=LEN
         SLR   R5,R1                    R5->START FIELD TO MOVE
         BCTR  R1,0
         L     R6,INSTSTR_SFPT          R6->CURR POINT TO MOVE
         EX    R1,MVC65
         LA    R1,1(R1,R6)              SET UP POINTER
         ST    R1,INSTSTR_SFPT
         B     A900                     SELECT
A100     EQU   *
         CLI   INSTSTR_SFLG,INSTSTR_SED2 WHEN EDIT IN TO MIN LEN
         BNE   A200
         L     R6,INSTSTR_SFPT
         MVC   INSTSTR_EDWK,EDM         MOVE IN EDIT MASK
         LA    R1,INSTSTR_EDWK+L'INSTSTR_EDWK-1
         EDMK  INSTSTR_EDWK,INSTSTR_SPNP EDIT IN FROM PACKED FIELD
         BNM   A120                     IF -VE
         MVI   0(R6),C'-'               MOVE IN -VE SIGN
         LA    R6,1(R6)                 SKIP UP FIELD
A120     EQU   *                        ENDIF
         LA    R4,INSTSTR_EDWK+L'INSTSTR_EDWK-1 R4->END EDIT FIELD-1
         SLR   R4,R1                    R4->LEN FIELD TO MOVE -1
         LR    R5,R1                    R5->DIGITS
         EX    R4,MVC65
         LA    R1,1(R4,R6)              SET UP POINTER
         ST    R1,INSTSTR_SFPT
         B     A900                     SELECT
A200     EQU   *
         CLI   INSTSTR_SFLG,INSTSTR_SCVX WHEN CONVHEX
         BNE   A300
         LM    R5,R8,INSTSTR_SFLD       R5->SOURCE,R6->RESULT,R8=LEN
         LA    R1,0(R6,R8)
         ST    R1,INSTSTR_SFPT
         SRL   R8,1
         LTR   R8,R8
         BNP   A300
*
CHAR_LOOP DS   0H                       LOOP
         SLR   R1,R1
         IC    R1,0(R5)
         SLL   R1,1                     R1 = OFFSET IN TAB
         LA    R9,XTAB(R1)
         MVC   0(2,R6),0(R9)
         LA    R5,1(R5)
         LA    R6,2(R6)
         BCT   R8,CHAR_LOOP

         B     A900                     SELECT
A300     EQU   *
*
*
*
A900     EQU   *
         INSTRS
EDM      DC    X'40',29X'20',X'2120'
MVC65    MVC   0(0,R6),0(R5)

XTAB     DC    C'000102030405060708090A0B0C0D0E0F'
         DC    C'101112131415161718191A1B1C1D1E1F'
         DC    C'202122232425262728292A2B2C2D2E2F'
         DC    C'303132333435363738393A3B3C3D3E3F'
         DC    C'404142434445464748494A4B4C4D4E4F'
         DC    C'505152535455565758595A5B5C5D5E5F'
         DC    C'606162636465666768696A6B6C6D6E6F'
         DC    C'707172737475767778797A7B7C7D7E7F'
         DC    C'808182838485868788898A8B8C8D8E8F'
         DC    C'909192939495969798999A9B9C9D9E9F'
         DC    C'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    C'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    C'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    C'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    C'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    C'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         LTORG

INSTSTR_D    DSECT
        COPY INSTSTRD

         END
