         MACRO
&NAME    INSTRS &RC=
.**********************************************************************
.*
.*  12/12/00  1.0    WRITTEN                                     AWR *
.*
.**********************************************************************
.*
.*
.* RETURN FROM RENT ASM PROGRAM
.*
.*
       GBLB   &INSTSAVE

&NAME    DS   0H
         AIF   ('&RC' EQ '').RCOK
         AIF   ('&RC' EQ '(R15)').RCOK
         AIF   ('&RC' EQ '(15)').RCOK
         AIF   ('&RC' EQ '0').RC15
         AIF   ('&RC' EQ '1').RC15
         AIF   ('&RC' EQ '2').RC15
         AIF   ('&RC' EQ '3').RC15
         AIF   ('&RC' EQ '4').RC15
         AIF   ('&RC' EQ '5').RC15
         AIF   ('&RC' EQ '6').RC15
         AIF   ('&RC' EQ '7').RC15
         AIF   ('&RC' EQ '8').RC15
         AIF   ('&RC' EQ '9').RC15
         AIF   ('&RC' EQ '10').RC15
         AIF   ('&RC' EQ '11').RC15
         AIF   ('&RC' EQ '12').RC15
         AIF   ('&RC' EQ '13').RC15
         AIF   ('&RC' EQ '14').RC15
         AIF   ('&RC' EQ '15').RC15
         AIF   ('&RC' EQ '16').RC15
       MNOTE 8,'YOU MAY ONLY SPECIFY RC=(R15) OR RC=(15) OR RC=0 TO 16'
         MEXIT
.RC15    ANOP
         LA    R15,&RC
.RCOK    ANOP



         AIF   (&INSTSAVE EQ 0).RET

         LR    R2,R15              SAVE R15
         LR    R1,R13              GET FREEMAIN ADDRESS
         L     R13,INSTWORK_SAVEBK R13 NOW OLD REG 13
         L     R0,=A(INSTWORK_DATALEN) SET GETMAIN LEN
         FREEMAIN RU,LV=(0),A=(1)   FREE DATA DSECT STORAGE
         LR    R15,R2              RC IN R15

.RET     ANOP
         L     R14,12(R13)         RESTORE R14
         LM    R0,R12,20(R13)      RESTORE R0-R14
         AIF   ('&RC' NE '').NOZERO
         SLR   R15,R15
.NOZERO  ANOP
         BSM   0,R14
         MEND
