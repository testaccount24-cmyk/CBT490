         MACRO
&NAME    INSTNUM &FIELD,&LEN=
&NAME    DS    0H



         LCLA  &SUBA,&RESN


         INSTREQU


         AIF   ('&FIELD' NE '').A
         MNOTE 4,'FIELD NOT SPECIFIED'
         MEXIT
.A       ANOP


.C       ANOP

         AIF   ('&LEN' NE '').LENB
         LA    R1,L'&FIELD
         AGO   .LENZ
.LENB    ANOP
         AIF   ('&LEN'(1,1) NE '(').LENC
         LR    R1,&LEN
         AGO   .LENZ
.LENC    ANOP
         LA    R1,&LEN
.LENZ    ANOP



.*  R1 HOLDS LENGTH

         LA    R14,&FIELD

.*  R14 IS FIELD POINTER

         LA    R15,0(R14,R1)

.*  R15 IS END OF FIELD





.*       INITIAL SET OF BLANKS
INSA&SYSNDX   EQU   *
         CR   R14,R15
         BNL  INS0&SYSNDX
         CLI  0(R14),C' '
         BNE  INSB&SYSNDX
         LA   R14,1(R14)
         B    INSA&SYSNDX
INSB&SYSNDX   EQU   *


.* NOW THERE MUST BE A STRING OF DIGITS


         CLI  0(R14),C'0'
         BL   INS0&SYSNDX
         CLI  0(R14),C'9'
         BH   INS0&SYSNDX



         SLR  R1,R1
         SLR  R0,R0
INSH&SYSNDX   EQU   *
         IC   R0,0(R14)
         SH   R0,=Y(X'F0')
         AR   R1,R0
         LA   R14,1(R14)
         CR   R14,R15
         BNL  INSJ&SYSNDX
         CLI  0(R14),C'0'
         BL   INSJ&SYSNDX
         CLI  0(R14),C'9'
         BH   INSJ&SYSNDX
         MH   R1,=Y(10)
         B    INSH&SYSNDX

INSJ&SYSNDX   EQU   *


INSM&SYSNDX   EQU   *
         CR   R14,R15
         BNL  INS9&SYSNDX
         CLI  0(R14),C' '
         BNE  INS0&SYSNDX
         LA   R14,1(R14)
         B    INSM&SYSNDX




INS0&SYSNDX   DS    0H BAD FIELD
         LA   R15,4
         B    INSZ&SYSNDX

INS9&SYSNDX   DS    0H ALL OK
         LA   R15,0


INSZ&SYSNDX   DS    0H
         MEND
         MACRO
